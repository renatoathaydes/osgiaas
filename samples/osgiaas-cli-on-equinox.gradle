/**
 * This Gradle build file uses the osgi-run plugin to create an OSGi environment that
 * includes the osgiaas-cli bundle, amongst other bundles (see the dependencies section).
 *
 * It runs inside a Equinox container (rather than the default Apache Felix).
 *
 * Build with:
 *     gradle -b osgiaas-cli-on-equinox.gradle createOsgiRuntime
 *
 * Then, run with:
 *     bash build/osgi/run.sh
 * or, in Windows:
 *     build/osgi/run.bat
 */

plugins {
    id "com.athaydes.osgi-run" version "1.5.4"
}

repositories {
    mavenLocal()
    jcenter()
}

final osgiaasVersion = '0.7'

dependencies {
    // OSGiaaS Modules
    osgiRuntime "com.athaydes.osgiaas:osgiaas-cli-core:$osgiaasVersion"
    osgiRuntime "com.athaydes.osgiaas:osgiaas-cli-ivy:$osgiaasVersion"
    osgiRuntime "com.athaydes.osgiaas:osgiaas-cli-groovy:$osgiaasVersion"
    osgiRuntime "com.athaydes.osgiaas:osgiaas-cli-javac:$osgiaasVersion"
    osgiRuntime "com.athaydes.osgiaas:osgiaas-cli-js:$osgiaasVersion"

    // OSGi Service Component Runtime implementation
    osgiRuntime 'org.apache.felix:org.apache.felix.scr:2.0.2', {
        exclude group: '*'
    }

    // Logging
    osgiRuntime 'org.apache.felix:org.apache.felix.log:1.0.1', {
        transitive = false
    }
    osgiRuntime 'com.athaydes.osgiaas:slf4j-to-osgi-log:1.7.0'

}

runOsgi {
    configSettings = 'equinox'

    config += [ 'org.osgi.framework.system.packages.extra': 'sun.reflect,jdk.nashorn.api.scripting' ]
    bundles = [ ] // all bundles added as osgiRuntime dependencies
}
