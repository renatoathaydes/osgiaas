/**
 * This Gradle build file uses the osgi-run plugin to create an OSGi environment that
 * includes the osgiaas-cli bundle, amongst other bundles (see the dependencies section).
 *
 * Build with:
 *     gradle -b osgiaas-cli.gradle createOsgiRuntime
 *
 * Then, run with:
 *     bash build/osgi/run.sh
 * or, in Windows:
 *     build/osgi/run.bat
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.athaydes.gradle.osgi:osgi-run-core:1.5.1"
    }
}

apply plugin: 'com.athaydes.osgi-run'

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    // the osgiaas-cli core bundle
    osgiRuntime 'com.athaydes.osgiaas:osgiaas-cli-core:1.0-SNAPSHOT', {
        exclude group: 'org.osgi'
    }

    // OSGi Declarative Services implementation
    osgiRuntime 'org.apache.felix:org.apache.felix.scr:2.0.2', {
        exclude group: '*'
    }

    // OSGi container
    osgiMain "org.apache.felix:org.apache.felix.main:5.4.0"
}

runOsgi {
    bundles = [ ] // all bundles added as osgiRuntime dependencies
}