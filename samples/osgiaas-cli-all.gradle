/**
 * This Gradle build file uses the osgi-run plugin to create an OSGi environment that
 * includes the osgiaas-cli bundle, amongst other bundles (see the dependencies section).
 *
 * Build with:
 *     gradle -b osgiaas-cli-all.gradle createOsgiRuntime
 *
 * Then, run with:
 *     bash build/osgi/run.sh
 * or, in Windows:
 *     build/osgi/run.bat
 */

plugins {
    id "com.athaydes.osgi-run" version "1.5.2"
}

repositories {
    mavenLocal()
    jcenter()
}

final osgiaasVersion = '0.5'

dependencies {
    // OSGiaaS Modules
    osgiRuntime "com.athaydes.osgiaas:osgiaas-cli-core:$osgiaasVersion"
    osgiRuntime "com.athaydes.osgiaas:osgiaas-cli-frege:$osgiaasVersion" // requires the Frege systemLib
    osgiRuntime "com.athaydes.osgiaas:osgiaas-cli-grab:$osgiaasVersion"
    osgiRuntime "com.athaydes.osgiaas:osgiaas-cli-ivy:$osgiaasVersion"
    osgiRuntime "com.athaydes.osgiaas:osgiaas-cli-groovy:$osgiaasVersion"
    osgiRuntime "com.athaydes.osgiaas:osgiaas-cli-javac:$osgiaasVersion"
    osgiRuntime "com.athaydes.osgiaas:osgiaas-cli-js:$osgiaasVersion"
    osgiRuntime "com.athaydes.osgiaas:osgiaas-cli-scala:$osgiaasVersion"

    // system libs
    systemLib 'org.frege-lang:frege-interpreter-core:1.2'
    systemLib 'org.scala-lang:scala-compiler:2.12.1'

    // OSGi Service Component Runtime implementation
    osgiRuntime 'org.apache.felix:org.apache.felix.scr:2.0.2', {
        exclude group: '*'
    }

    // OBR CLI commands
    osgiRuntime 'org.apache.felix:org.apache.felix.bundlerepository:2.0.6', {
        exclude group: '*'
    }

    // Logging
    osgiRuntime 'org.apache.felix:org.apache.felix.log:1.0.1', {
        transitive = false
    }
    osgiRuntime 'com.athaydes.osgiaas:slf4j-to-osgi-log:1.7.0'

    // OSGi container
    osgiMain "org.apache.felix:org.apache.felix.main:5.4.0"
}

runOsgi {
    bundles = [ ] // all bundles added as osgiRuntime dependencies

    // required to run JavaScript and Groovy
    config += [ 'org.osgi.framework.system.packages.extra': 'sun.reflect,jdk.nashorn.api.scripting' ]
}